<!DOCTYPE html>
<html lang="en">
<head>
	<meta charset="utf-8">
	<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
	<style>
		body {
			margin: 0;
			overflow: hidden;
		}
	</style>
</head>
<body></body>
<script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/97/three.min.js"></script>
<script id="vertexShader" type="x-shader/x-vertex">
	uniform float time;
	uniform vec2 resolution;
	void main()	{
		gl_Position = vec4(position, 1.0);
	}
</script>
<script id="fragmentShader" type="x-shader/x-fragment">
	uniform float time;
	uniform vec2 resolution;

	const int MAX_MARCHING_STEPS = 255;
	const float MIN_DIST = 0.0;
	const float MAX_DIST = 100.0;
	const float EPSILON = 0.001;

	float intersectSDF(float distA, float distB) {
	    return max(distA, distB);
	}

	float unionSDF(float distA, float distB) {
	    return min(distA, distB);
	}

	float differenceSDF(float distA, float distB) {
	    return max(distA, -distB);
	}

	float sphereSDF(vec3 samplePoint, float r) {
	    return length(samplePoint) - r;
	}

	float cubeSDF(vec3 p, float r) {
	    // If d.x < 0, then -1 < p.x < 1, and same logic applies to p.y, p.z
	    // So if all components of d are negative, then p is inside the unit cube
	    vec3 d = abs(p) - vec3(r, r, r);

	    // Assuming p is inside the cube, how far is it from the surface?
	    // Result will be negative or zero.
	    float insideDistance = min(max(d.x, max(d.y, d.z)), 0.0);

	    // Assuming p is outside the cube, how far is it from the surface?
	    // Result will be positive or zero.
	    float outsideDistance = length(max(d, 0.0));

	    return insideDistance + outsideDistance;
	}

	float sceneSDF(vec3 samplePoint) {
		float d = sphereSDF(samplePoint, 0.3);
		return d;
	}

	vec3 estimateNormal(vec3 p) {
	    return normalize(vec3(
	        sceneSDF(vec3(p.x + EPSILON, p.y, p.z)) - sceneSDF(vec3(p.x - EPSILON, p.y, p.z)),
	        sceneSDF(vec3(p.x, p.y + EPSILON, p.z)) - sceneSDF(vec3(p.x, p.y - EPSILON, p.z)),
	        sceneSDF(vec3(p.x, p.y, p.z  + EPSILON)) - sceneSDF(vec3(p.x, p.y, p.z - EPSILON))
	    ));
	}

	vec3 phongContribForLight(vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye,
	                          vec3 lightPos, vec3 lightIntensity) {
	    vec3 N = estimateNormal(p);
	    vec3 L = normalize(lightPos - p);
	    vec3 V = normalize(eye - p);
	    vec3 R = normalize(reflect(-L, N));

	    float dotLN = dot(L, N);
	    float dotRV = dot(R, V);

	    if (dotLN < 0.0) {
	        // Light not visible from this point on the surface
	        return vec3(0.0, 0.0, 0.0);
	    }

	    if (dotRV < 0.0) {
	        // Light reflection in opposite direction as viewer, apply only diffuse
	        // component
	        return lightIntensity * (k_d * dotLN);
	    }
	    return lightIntensity * (k_d * dotLN + k_s * pow(dotRV, alpha));
	}

	/**
	 * Lighting via Phong illumination.
	 *
	 * The vec3 returned is the RGB color of that point after lighting is applied.
	 * k_a: Ambient color
	 * k_d: Diffuse color
	 * k_s: Specular color
	 * alpha: Shininess coefficient
	 * p: position of point being lit
	 * eye: the position of the camera
	 *
	 * See https://en.wikipedia.org/wiki/Phong_reflection_model#Description
	 */
	vec3 phongIllumination(vec3 k_a, vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye) {
	    const vec3 ambientLight = 0.5 * vec3(1.0, 1.0, 1.0);
	    vec3 color = ambientLight * k_a;

	    vec3 light1Pos = vec3(4.0 * sin(time/1000.0),
	                          2.0,
	                          4.0 * cos(time/1000.0));
	    vec3 light1Intensity = vec3(0.4, 0.4, 0.4);

	    color += phongContribForLight(k_d, k_s, alpha, p, eye,
	                                  light1Pos,
	                                  light1Intensity);

	    vec3 light2Pos = vec3(2.0 * sin(0.000037 * time),
	                          2.0 * cos(0.000037 * time),
	                          2.0);
	    vec3 light2Intensity = vec3(0.9, 0.4, 0.3);

	    color += phongContribForLight(k_d, k_s, alpha, p, eye,
	                                  light2Pos,
	                                  light2Intensity);
	    return color;
	}

	vec3 rayDirection(float fieldOfView, vec2 size, vec2 fragCoord) {
	    vec2 xy = fragCoord - size / 2.0;
	    float z = size.y / tan(radians(fieldOfView) / 2.0);
	    return normalize(vec3(xy, -z));
	}

	float shortestDistanceToSurface(vec3 eye, vec3 marchingDirection, float start, float end) {
	    float depth = start;
	    for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
	        float dist = sceneSDF(eye + depth * marchingDirection);
	        if (dist < EPSILON) {
				return depth;
	        }
	        depth += dist;
	        if (depth >= end) {
	            return end;
	        }
	    }
	    return end;
	}


	mat4 vm(vec3 eye, vec3 center, vec3 up) {
	    // Based on gluLookAt man page
	    vec3 f = normalize(center - eye);
	    vec3 s = normalize(cross(f, up));
	    vec3 u = cross(s, f);
	    return mat4(
	        vec4(s, 0.0),
	        vec4(u, 0.0),
	        vec4(-f, 0.0),
	        vec4(0.0, 0.0, 0.0, 1)
	    );
	}

	void main()	{
		vec3 viewDir = rayDirection(45.0, resolution, gl_FragCoord.xy);
		vec3 eye = vec3(2.0, 2.0, 5.0);

		mat4 viewToWorld = vm(eye, vec3(0.0, 0.0, 0.0), vec3(0.0, 1.0, 0.0));

    vec3 worldDir = (viewToWorld * vec4(viewDir, 0.0)).xyz;

    float dist = shortestDistanceToSurface(eye, worldDir, MIN_DIST, MAX_DIST);

		if(dist > MAX_DIST - EPSILON) {
			gl_FragColor = vec4(1.0, 1.0, 1.0, 0.0);
			return;
		}

		vec3 p = eye + dist * worldDir;

    vec3 K_a = vec3(0.2, 0.2, 0.2);
    vec3 K_d = vec3(0.7, 0.2, 0.2);
    vec3 K_s = vec3(1.0, 1.0, 1.0);
    float shininess = 10.0;

    vec3 color = phongIllumination(K_a, K_d, K_s, shininess, p, eye);

		gl_FragColor = vec4(color, 1.0);
	}
</script>
<script>
	var container;
	var camera, scene, renderer;
	var uniforms, material, mesh;
	var mouseX = 0, mouseY = 0, lat = 0, lon = 0, phy = 0, theta = 0;

	var windowHalfX = window.innerWidth / 2;
	var windowHalfY = window.innerHeight / 2;

	init();
	var startTime = Date.now();
	animate();

	function init() {
		camera = new THREE.Camera();
		camera.position.z = 1;
		scene = new THREE.Scene();

		uniforms = {
			time: { type: "f", value: 1.0 },
			resolution: { type: "v2", value: new THREE.Vector2() }
		};

		material = new THREE.ShaderMaterial( {
			uniforms: uniforms,
			vertexShader: document.getElementById('vertexShader').textContent,
			fragmentShader: document.getElementById('fragmentShader').textContent
		});

		/*
		var axesHelper = new THREE.AxesHelper( 5 );
		scene.add( axesHelper );
		*/

		mesh = new THREE.Mesh(new THREE.PlaneGeometry(2, 2), material);
		scene.add(mesh);

		renderer = new THREE.WebGLRenderer();
		document.body.appendChild( renderer.domElement );

		uniforms.resolution.value.x = window.innerWidth;
		uniforms.resolution.value.y = window.innerHeight;
		renderer.setSize( window.innerWidth, window.innerHeight );
	}

	function animate() {
		requestAnimationFrame( animate );
		render();
	}

	function render() {
		var elapsedMilliseconds = Date.now() - startTime;
		var elapsedSeconds = elapsedMilliseconds / 1000.;
		uniforms.time.value = 60. * elapsedSeconds;
		renderer.render( scene, camera );
	}
</script>
</html>
